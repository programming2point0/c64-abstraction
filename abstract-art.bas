10 X=0:Y=0:R=0:T=0:C=0:F=0
20 COLOUR 1,1:HIRES 0,1:MULTI 0,2,6
30 REM DECIDE ON COLOR
31 IFRND(1)>.05GOTO35
32 REM 5% CHANCE FOR NEW COLORS
33 LOW COL 0,INT(RND(1)*15),INT(RND(1)*15)
35 C = INT(RND(1)*3)+1
40 REM DECIDE ON TYPE
41 T=RND(1)
42 IFT<.35THENGOSUB200:GOTO60
43 IFT<.66THENGOSUB300:GOTO50
44 IFT<1THENGOSUB400:GOTO50
45 REM - ADD TRIANGLE! -
50 REM DECIDE TO FILL OR NOT
51 IF R>100 OR RND(1)<.5 GOTO60
52 F=C+1:IFF=4THENF=1
53 PAINT X+1,Y+1,F
60 FORA=0TO300:NEXTA
70 GOTO 30
200 REM *** LINE ***
210 X1=INT(RND(1)*160):Y1=INT(RND(1)*200)
220 X2=INT(RND(1)*160):Y2=INT(RND(1)*200)
230 LINE X1,Y1,X2,Y2,C
240 RETURN
300 REM *** CIRCLE ***
310 X=8+INT(RND(1)*152):Y=8+INT(RND(1)*192)
320 R=4+INT(RND(1)*40)
330 CIRCLE X,Y,R,R*1.6,C
340 RETURN
400 REM *** RECTANGLE ***
410 X=INT(RND(1)*152):Y=INT(RND(1)*192)
420 W=8+INT(RND(1)*(152-X)):H=8+INT(RND(1)*(192-Y))
430 REC X,Y,W,H,C
440 RETURN
